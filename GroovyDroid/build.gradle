buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.0'
    }
}
apply plugin: 'android'

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
	compile 'com.google.android.tools:dx:1.7'

    compile 'org.codehaus.groovy:groovy:2.3.0-droid-SNAPSHOT'
}

android {
    compileSdkVersion 17
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 17
    }
}

android.applicationVariants.all {
    task "groovy${name}Compile"(type:GroovyCompile) {
        source = javaCompile.source + fileTree('src/main/java').include('**/*.groovy')
        destinationDir = javaCompile.destinationDir
        classpath = javaCompile.classpath
        groovyClasspath = classpath
        sourceCompatibility = '1.6'
        targetCompatibility = '1.6'
        doFirst {
            def runtimeJars = plugins.findPlugin(com.android.build.gradle.AppPlugin).runtimeJars
            classpath = files(runtimeJars) + classpath
        }
    }
    javaCompile.dependsOn("groovy${name}Compile")
    javaCompile.enabled = false
}
